/*
/ Program name: meanci.sas
/
/ Program version: 2.1
/
/ Program purpose: Macro to compute mean values for two groups of data,
/                  estimate of difference between means, and 95% confidence
/                  interval for difference between means
/
/
/ SAS version: 6.12 TS020
/
/ Created by: Les Huson, CSDH Clinical Statistics Section
/ Date:       03/9/92
/
/ Input parameters: data    - name of SAS dataset containing AT LEAST a group variable
/                             and a variable containing data values
/                   group   - name of the group variable in the input data set
/                   level1  - value of level1 in the group variable
/                   level2  - value of level2 in the group variable
/                   var     - name of the variable containing data values
/                   datid   - an id for the particular data set being processed - this
/                             appears in the output data set, and identifies particular
/                             sets of results in cases where this macro might be called
/                             several times
/                   out     - the name of an output data set contining the key results
/                             generated by the macro
/                   pfx     - Prefix for temporary data set names
/                   tidy    - Delete temporary data sets?
/                   verbose - Verbosity of macro messages
/
/
/ Output created:
/
/ Macros called:
/
/ Example call:
/
/                 %meanci(data=datad01,group=groupid,var=value);
/
/===========================================================================================
/ Change log:
/
/    MODIFIED BY: ABR
/    DATE:        22/ 9/92
/    MODID:       Ver 1.1
/    DESCRIPTION: Improve efficiency.
/    -----------------------------------------------------------------------
/    MODIFIED BY: ABR
/    DATE:        02/11/92
/    MODID:       Ver 1.3
/    DESCRIPTION: Use NUM_A/B instead of NA/B to avoid conflicts with names
/                 of measurement variables.
/                 Add PFX, TIDY, and VERBOSE options.
/    -----------------------------------------------------------------------
/    MODIFIED BY: Jonathan Fry
/    DATE:        10DEC1998
/    MODID:       JMF001
/    DESCRIPTION: Tested for Y2K compliance.
/                 Add %PUT statement for Macro Name and Version Number.
/                 Change Version Number to 2.1.
/    -----------------------------------------------------------------------
/    MODIFIED BY:
/    DATE:
/    MODID:       XXX002
/    DESCRIPTION:
/    -----------------------------------------------------------------------
/============================================================================================*/

%MACRO meanci (data=temp1
              ,group=trtseq
              ,level1=A,level2=B
              ,var=
              ,datid=results
              ,out=temp2
              ,pfx=MEN_
              ,tidy=y
              ,verbose=2
              );

      /*
      / JMF001
      / Display Macro Name and Version Number in LOG
      /-----------------------------------------------------------------*/

      %put ----------------------------------------------------;
      %put NOTE: Macro called: MEANCI.SAS   Version Number: 2.1;
      %put ----------------------------------------------------;


*
     First, check-out parameters.
;
 %if &tidy ne Y and &tidy ne N %then %do;
   %put WARNING: MEANCI: "&tidy" is not a valid value for TIDY.;
   %let tidy = Y;
   %put WARNING: MEANCI: TIDY has been set to "&tidy".;
   %end;
 %if %length(&pfx) gt 4 %then %do;
   %put WARNING: MEANCI: "&pfx" exceeds maximum length for PFX.;
   %let pfx = %substr(&pfx,1,4);
   %put WARNING: MEANCI: PFX has been truncated to "&pfx".;
   %end;
 %if %index(0123456789,&verbose) eq 0 %then %do;
   %put WARNING: MEANCI: "&verbose" is an invalid value for VERBOSE.;
   %let verbose = 2;
   %put WARNING: MEANCI: VERBOSE has been set to &verbose.;
   %end;


*
     Pick out just level 1 data values, finding the mean etc.
     etc.
;
PROC UNIVARIATE DATA=&data (WHERE=(&var NE . and
                                   &group EQ "&level1"
                                  )
                           )
                NOPRINT
                ;
     VAR &var;
     OUTPUT OUT=&pfx.a010 mean=meana var=vara nobs=num_a;
     RUN;


*
     Pick out level 2 data values, finding the mean etc.
;
PROC UNIVARIATE DATA=&data (WHERE=(&var NE . and
                                   &group EQ "&level2"
                                  )
                           )
                NOPRINT
                ;
     VAR &var;
     OUTPUT OUT=&pfx.b010 mean=meanb var=varb nobs=num_b;
     RUN;


*
    Get the means etc. back from seperate data sets and
    put the results into the output data set
;
DATA &out   (keep = label meana meanb num_a num_b diff lower upper);
     LENGTH label $20;
     MERGE &pfx.a010 &pfx.b010;
     label = "&datid";
     diff   = meana - meanb;
     sediff = SQRT((vara/num_a)+(varb/num_b));
     lower  = diff - (1.96*sediff);
     upper  = diff + (1.96*sediff);
     IF lower = . THEN upper = .;
     ELSE IF upper = . THEN lower = .;
     IF ( (num_a < 5) AND (num_b < 5) ) THEN DO;
         diff = .;
         lower = .;
         upper = .;
     END;
     RUN;


*
     Get rid of all the working data sets
;
%if &tidy eq Y %then %do;
  PROC DATASETS LIBRARY=WORK NOLIST;
       DELETE &pfx.a010 &pfx.b010;
       RUN;
  %end;


*
     end of macro
;
%MEND meanci;
